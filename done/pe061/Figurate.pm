#!/usr/bin/perl -l
# vim: set ai et ts=4 tw=75 :

use strict;
use warnings FATAL => 'all';
use Data::Dumper;
use List::Util 'sum';

my %N = f();

my @cand;
for my $f (@{ $N{3} }) {
    push @cand, [ [ 3 => $f ] ];
}

while (@cand) {
    print "remaining=", scalar(@cand);
    my @c = @{ shift @cand };
    print "trying: ", as_string(@c);
    if (@c == 6) {
        my $head = substr($c[0][1],0,2);
        my $tail = substr($c[-1][1],-2);
        if ($head == $tail) {
            print "sum=", sum map { $_->[1] } @c;
            last;
        }
    }
    my @n = do {
        my %seen = map { $_->[0] => 1 } @c;
        grep { !$seen{$_} } 3 .. 8;
    };
    my $tail = substr($c[-1][1],-2);
    for my $n (@n) {
        for my $f (@{ $N{$n} }) {
            next unless $f =~ /^$tail/;
            my $new = [ @c, [ $n => $f ] ];
            print "new: ", as_string(@$new);
            push @cand, $new;
        }
    }
}

sub as_string {
    return join q( ), map "($_->[0]=>$_->[1])", @_;
}

sub f {
    my %n;
    for my $i (3 .. 8) {
        for (my $j = 1; P($i,$j) < 10_000; $j++) {
            my $p = P($i,$j);
            next if $p < 1_000;
            next if $p =~ /^..0.$/;
            push @{ $n{$i} }, $p;
        }
    }
    return %n;
}

sub P {
    my ($p, $n) = @_;
    return $n*($n+1)/2      if $p == 3;
    return $n*$n            if $p == 4;
    return $n*(3*$n-1)/2    if $p == 5;
    return $n*(2*$n-1)      if $p == 6;
    return $n*(5*$n-3)/2    if $p == 7;
    return $n*(3*$n-2)      if $p == 8;
    die "invalid p=$p";
}


__END__

http://projecteuler.net/index.php?section=problems&id=61

Problem 61
16 January 2004

Triangle, square, pentagonal, hexagonal, heptagonal, and octagonal numbers
are all figurate (polygonal) numbers and are generated by the following
formulae:

    Triangle    P(3,n)=n(n+1)/2     1, 3, 6, 10, 15, ...
    Square      P(4,n)=n^2          1, 4, 9, 16, 25, ...
    Pentagonal  P(5,n)=n(3n-1)/2    1, 5, 12, 22, 35, ...
    Hexagonal   P(6,n)=n(2n-1)      1, 6, 15, 28, 45, ...
    Heptagonal  P(7,n)=n(5n-3)/2    1, 7, 18, 34, 55, ...
    Octagonal   P(8,n)=n(3n-2)      1, 8, 21, 40, 65, ...

The ordered set of three 4-digit numbers: 8128, 2882, 8281, has three
interesting properties.

    1. The set is cyclic, in that the last two digits of each number is
       the first two digits of the next number (including the last number
       with the first).
    2. Each polygonal type: triangle (P(3,127)=8128), square
       (P(4,91)=8281), and pentagonal (P(5,44)=2882), is represented by a
       different number in the set.
    3. This is the only set of 4-digit numbers with this property.

Find the sum of the only ordered set of six cyclic 4-digit numbers for
which each polygonal type: triangle, square, pentagonal, hexagonal,
heptagonal, and octagonal, is represented by a different number in the set.

